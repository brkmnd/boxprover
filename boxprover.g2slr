#grammar for boxprover
prec "not" 7
prec "all" 6
prec "exi" 5
prec "eq" 4
prec "and" 3
prec "or" 2
prec "imp" 1
assoc "left"  : "and","or","not","eq"
assoc "right" : "imp"
token  "and","or","imp","not","eq" as "/\\\\","\\\\/","=>|->","~","==|="
token "prem","assump" as "premise;","assumption;"
token 
    "all_e","all_i","bot_e","con_e1","con_e2","con_i","dis_e",
    "dis_i1","dis_i2","eq_e","eq_i","exi_e","exi_i","imp_e","imp_i",
    "neg_e","neg_i","nne","nni","pbc","top_i","var","all","exi",
    "lem","mt","by","copy"
    as
    "all_e(?= )","all_i(?= )","bot_e(?= )","con_e1(?= )","con_e2(?= )","con_i(?= )","dis_e(?= )",
    "dis_i1(?= )","dis_i2(?= )","eq_e(?= )","eq_i(?= )","exi_e(?= )","exi_i(?= )","imp_e(?= )","imp_i(?= )",
    "neg_e(?= )","neg_i(?= )","nne(?= )","nni(?= )","pbc(?= )","top_i(?= )","var(?= )","all(?= )","exi(?= )",
    "lem(?= )","mt(?= )","by(?= )","copy(?= )"

token "lpar","rpar","lbracket","rbracket" as "(",")","\\[","\\]"
token "uscore","scolon","dot" as "_",";","."
token -cap "name" as "[a-zA-Z0-9@#\\-_'\\^]+|q[^q]*q"
!token "\n","\r","\t"," "

prod Lines      -> Line LinesOptEnd
prod LinesOptEnd-> "scolon" Naming Lines
                 | Naming Lines
                 | Lines
                 | "dot"
                 |
prod Line       -> Formula Rule
                 | "lpar" Box "rpar"
                 | "uscore"
prod Rule       -> "prem" | "assump" | "by" RuleApply
prod Box        -> "var" Naming Lines
                 | Lines
prod RuleApply  -> "top_i"
                 | "con_i" "name" "name"
                 | "con_e1" "name"
                 | "con_e2" "name"
                 | "dis_i1" "name"
                 | "dis_i2" "name"
                 | "dis_e" "name" "name" "name"
                 | "imp_i" "name"
                 | "imp_e" "name" "name"
                 | "neg_i" "name"
                 | "neg_e" "name" "name"
                 | "bot_e" "name"
                 | "nne" "name"
                 | "nni" "name"
                 | "lem"
                 | "pbc" "name"
                 | "mt" "name" "name"
                 | "all_i" "name"
                 | "all_e" "name" "name"
                 | "exi_i" "name" "name"
                 | "exi_e" "name" "name"
                 | "eq_i"
                 | "eq_e" Bind "name" "name"
                 | "copy" "name"
prod Naming     -> "lbracket" "name" "rbracket"
prod Formula    -> Formula "and" Formula
                 | Formula "or" Formula
                 | Formula "imp" Formula
                 | Formula "eq" Formula
                 | "all" Bind
                 | "exi" Bind
                 | "not" Formula
                 | Atom
prod Bind       -> "lpar" Naming Formula "rpar" 
prod Atom       -> "lpar" Formula "rpar"
                 | "name" OptArgs
prod OptArgs    -> "name" OptArgs
                 | "lpar" Formula "rpar" OptArgs
                 |
